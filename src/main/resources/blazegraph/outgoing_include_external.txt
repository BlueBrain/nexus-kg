prefix nxv: <https://bluebrain.github.io/nexus/vocabulary/>

SELECT ?total ?s ?property ?type ?_self ?_constrainedBy ?_project ?_rev ?_deprecated ?_createdAt  ?_createdBy ?_updatedAt ?_updatedBy

WITH {
  SELECT DISTINCT ?property ?s ?type ?_self ?_constrainedBy ?_project ?_rev ?_deprecated ?_createdAt  ?_createdBy ?_updatedAt ?_updatedBy WHERE {
    GRAPH <{id}/graph> {
      ?ss ?property ?s .
      FILTER(isIri(?s) && ?property NOT IN (nxv:updatedBy, nxv:createdBy, nxv:constrainedBy, nxv:project, rdf:type, nxv:self, nxv:incoming, nxv:outgoing))
    } .
    OPTIONAL {
      GRAPH ?g2 {
      ?s nxv:self ?_self .
      ?s nxv:constrainedBy ?_constrainedBy .
      ?s nxv:project ?_project .
      ?s nxv:rev ?_rev .
      ?s nxv:deprecated ?_deprecated .
      ?s nxv:createdAt ?_createdAt .
      ?s nxv:createdBy ?_createdBy .
      ?s nxv:updatedAt ?_updatedAt .
      ?s nxv:updatedBy ?_updatedBy .
      ?s rdf:type ?type .
        FILTER(?g2 != <{id}/graph>)
      }
    } .
    OPTIONAL {
      GRAPH <{id}/graph>  { ?s rdf:type ?type }
    }
  }
} AS %resultSet


WHERE {
   {
      SELECT (COUNT(?s) AS ?total)
      WHERE { INCLUDE %resultSet }
   }
   UNION
  {
      SELECT *
      WHERE { INCLUDE %resultSet }
      ORDER BY ?s ?property
      LIMIT {size}
      OFFSET {offset}
   }
}